#include <Arduino.h>

#define BUZZER_PIN 32
#define BUTTON_PIN 27

#define C6  1047
#define D6  1175
#define E6  1319
#define F6  1397
#define G6  1568
#define A6  1760
#define B6  1976
#define C7  2093

int melody[] = {
  C6, C6, C6,   C6, A6,   G6, A6, F6,
  C6, C6, C6,   C6, A6,   G6, A6, F6,
  C6, A6,   G6, A6,   F6, G6,
  F6, D6, F6, F6,   F6, D6, F6,
  F6, D6, F6, F6,   F6, D6, F6,
  C6, D6, C6, A6,   G6, F6
};

int noteCount = sizeof(melody) / sizeof(int);

int speedTable[5] = { 1000, 600, 300, 150, 70 };
const char* speedName[5] = { "ช้ามาก", "ช้า", "ปกติ", "เร็ว", "เร็วมาก" };
int speedIdx = 2;
int activeDuration;

hw_timer_t *timer = NULL;
volatile bool buzzerState = false;

int currentNote = 0;
unsigned long lastNoteTime = 0;
bool silentGap = false;

void IRAM_ATTR onTimer() {
  buzzerState = !buzzerState;
  digitalWrite(BUZZER_PIN, buzzerState);
}

void playTone(int freq) {
  if (freq <= 0) {
    timerStop(timer);
    digitalWrite(BUZZER_PIN, LOW);
    return;
  }
  uint64_t alarm = 1000000ULL / (freq * 2);
  timerAlarm(timer, alarm, true, 0);
  timerStart(timer);
}

void stopTone() {
  timerStop(timer);
  digitalWrite(BUZZER_PIN, LOW);
}

void setup() {
  Serial.begin(9600);
  pinMode(BUZZER_PIN, OUTPUT);
  pinMode(BUTTON_PIN, INPUT_PULLUP);

  timer = timerBegin(1000000);
  timerAttachInterrupt(timer, &onTimer);

  activeDuration = speedTable[speedIdx];

  Serial.println("System Ready - Elephant Song (Recorder Notes)");
}

void loop() {
  static bool lastButton = HIGH;
  static bool pressed = false;
  static unsigned long pressTime = 0;
  const unsigned long debounceDelay = 200;

  bool nowButton = digitalRead(BUTTON_PIN);

  if (lastButton == HIGH && nowButton == LOW) {
    pressTime = millis();
    pressed = true;
  }

  if (lastButton == LOW && nowButton == HIGH && pressed) {
    if (millis() - pressTime > debounceDelay) {
      speedIdx = (speedIdx + 1) % 5;
      activeDuration = speedTable[speedIdx];
      Serial.printf(">>> ความเร็ว: %s (%d ms)\n",
                    speedName[speedIdx], activeDuration);
    }
    pressed = false;
  }

  lastButton = nowButton;

  unsigned long now = millis();

  if (!silentGap) {
    playTone(melody[currentNote]);
    if (now - lastNoteTime >= activeDuration) {
      stopTone();
      silentGap = true;
      lastNoteTime = now;
    }
  } else {
    if (now - lastNoteTime >= activeDuration / 3) {
      silentGap = false;
      currentNote = (currentNote + 1) % noteCount;
      lastNoteTime = now;
    }
  }
}
